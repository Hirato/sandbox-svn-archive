//basic day-night cycle map

light_1 = [ 1 ] //reduce light-radii to below activation threshold
mapstart = $lastmillis
daylength = 120000
moon = 0
entropy = 0

shiftcolour = [
	local init1 init2 init3
	local final1 final2 final3

	splitcolour init $arg1
	splitcolour final $arg2

	loop i 3 [
		id = (+ $i 1)
		cond [ || [! (rnd 2)] [= $[final@id] $[init@id]] ] [
			//dummy
		] [ > $[final@id] $[init@id] ] [
			[init@id] = (+ $[init@id] 1)
			entropy = (-f $entropy 1)
		] [ 1 ] [ //[ < $[final@i] $[init@i] ]
			[init@id] = (- $[init@id] 1)
			entropy = (-f $entropy 1)
		]
	]

	result (mergecolour init)
]

cycle = [
	local elapsed dawnborder dayborder duskborder nightborder
	local sunprogress

	dawnborder = (abs (*f $daylength .15))
	dayborder = (abs (*f $daylength .20))
	duskborder = (abs (*f $daylength .70))
	nightborder = (abs (*f $daylength .75))

	elapsed = (mod (+ (- $lastmillis $mapstart) $dawnborder) $daylength)

	//todo
	//fogcolour + fogquantity

	cond [ || (< $elapsed $dawnborder) (>= $elapsed $nightborder) ] [
		//night time
		if (>= $elapsed (+f $nightborder (*f .02 $daylength))) [
			light_1 = []
		] [
			light_1 = [ 1 ]
		]
		if $moon [
			ambient (shiftcolour $ambient 0x0A0A0A)
			sunlight (shiftcolour $sunlight 0x3F3F3F)
			cloudcolour (shiftcolour $cloudcolour 0x4F4F4F)
			skyboxcolour (shiftcolour $skyboxcolour 0x3F3F3F)
		] [
			ambient (shiftcolour $ambient 0x010101)
			sunlight (shiftcolour $sunlight 0x0)
			cloudcolour (shiftcolour $cloudcolour 0x0F0F0F)
			skyboxcolour (shiftcolour $skyboxcolour 0x0)
		]
	] [ < $elapsed $dayborder ] [
		//dawn
		light_1 = [ 1 ]
		sunlightyaw 260
		sunlightpitch 10

		ambient (shiftcolour $ambient 0x101010)
		sunlight (shiftcolour $sunlight 0xAF7F3F)
		cloudcolour (shiftcolour $cloudcolour 0x6F6F4F)
		skyboxcolour (shiftcolour $skyboxcolour 0xAF8F3F)
	] [ < $elapsed $duskborder ] [
		//day time
		light_1 = [ 1 ]
		sunprogress = (divf (-f $elapsed $dayborder) (-f $duskborder $dayborder))

		sunlightyaw (-f 260 (*f $sunprogress 160))
		sunlightpitch (+f 10 (*f 100 (? (>f $sunprogress .5) (-f 1 $sunprogress) $sunprogress)))

		ambient (shiftcolour $ambient 0x191919)
		sunlight (shiftcolour $sunlight 0xFFDFBF)
		cloudcolour (shiftcolour $cloudcolour 0xFFFFFF)
		skyboxcolour (shiftcolour $skyboxcolour 0xFFFFFF)
	] [ 1 ] [
		//dusk
		light_1 = [ 1 ]
		sunlightyaw 100
		sunlightpitch 10

		ambient (shiftcolour $ambient 0x101010)
		sunlight (shiftcolour $sunlight 0xAF7F3F)
		cloudcolour (shiftcolour $cloudcolour 0x6F6F4F)
		skyboxcolour (shiftcolour $skyboxcolour 0xAF8F3F)
	]

	//loop!
	sleep 0 cycle
]
cycle
