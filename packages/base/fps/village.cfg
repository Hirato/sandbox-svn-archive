piequests = 0
totalquests = 0
piequest1 = 0
pickedapples1 = 0
bakerspeak = [part_text_0 = $arg1; sleep $arg2 [part_text_0 = ""]]
level_trigger_21 = [showgui npc1-1]
level_trigger_23 = [if (= $piequest1 2) [showgui npc2-6 ] [showgui npc2-1]]
level_trigger_22 = [echo "These pies smell HEAVENLY!"]
johnact1 = [part_text_2 = $arg1; sleep $arg2 [part_text_2 = ""]]
//music Last-Flight nextsong

part_text_0 = ""
part_text_1 = ""
part_text_2 = ""

loop i 20 [
	(format "gotapple%1" (+ $i 1)) = 0
	(format "level_trigger_%1" (+ $i 1)) = (format "pickup %1" (+ $i 1))
]
apples = 0
totalapples = 0
pickedapples1 = 0 
applequests = 0 
piequests = 0 
piequest1 = 0 
pie1ready = 0 
pies = 0 
finishedquests = 0 
totalquests = 0 
money = 0 
totalpies = 0

finishedpie1 = 0

newgui main [
	guilist [
		guilist [
			guibutton "Quest information" "showgui quests"
			guibutton "Inventory" "showgui inventory"
		]
		guibar
		guilist [
			guibutton "Unload Village's UI" "newgui main [@main]"
		]
	]
	guibar
	@main
]

newgui inventory [
	guibutton "Back" "cleargui 1"
	guibar
	guitext (format "You currently carry %1 ogrons with you" $money)
	guitext (format "You currently carry %1 items" (+ $pies $apples))
	
	guitab "Temporary"
	if (> $apples 0) [
		guitext (format "You currently have %1 apples." $apples)
	]
	if (> $pies 0) [
		guitext (format "You currently carry %1 pies." $pies)
	]
	
]

newgui quests [
	guibutton "Back" "cleargui 1"
	guibar
	guitext (format "You've obtained %1 out of 2 quests" $totalquests)
	guitext (format "You've finished %1 out of 2 quests" $finishedquests)
	guibar
	guitext (format "You've obtained %1 out of 20 apples" $totalapples)
	guitext (format "You've obtained %1 out of 1 pies" $totalpies)
	
	guitab "Log"
	if $pickedapples1 [
		guibutton "The Baker's Apples" "showgui quest1" (if (< $pickedapples1 2) [result "exit"] [result "action"]) (if (< $pickedapples1 2) 0xFF6040 0x40FF50)
	]
	if $piequest1 [
		guibutton "A Pie for a Stranger" "showgui quest2" (if (< $piequest1 2) [result "exit"] [result "action"]) (if (< $piequest1 2) 0xFF6040 0x40FF50)
	]
]

newgui quest1 [
	if (= $pie1ready 2) [
		guitext "I collected the pie from the baker"
		guitext "It smells so good...."
		guibar
	]
	if $pie1ready [
		guitext "The pie he's baking for me is probably done by now. "
		guitext "I should go check up on him"
		guibar
	]
	if (= $pickedapples1 2) [
		guitext "How nice of him, he's baking me an apple pie"
		guitext "I should probably go exploring while I wait"
		guibar
	]
	if (|| (> $apples 14) (= $pickedapples1 2)) [
		guitext "I should have enough apples for the baker now"
	] [
		guitext "I should keep looking for more apples"
		if $grass [guitext "TIP: This will be easier with grass turned off"]
	]
	guibar 
	if $pickedapples1 [
		guitext "The baker is a bit low on supplies for his baked goods"
		guitext "He'd like me to fetch him 15 apples"
	] [
		echo "3HEY! NO CHEATING!"
	]
] "The Baker's Apples"

newgui quest2 [
	if (= $piequest1 2) [
		guitext "He was literally in tears when I gave him the pie."
		guitext "He's also eating it in a very beast like manner"
		guitext "He introduced himself properly too! his name is John!"
		guibar
	]
	if (|| $pies (= $piequest1 2)) [
		guitext "I got the pie... it smells wonderful!"
		guitext "I should probably get back to the strange fellow"
		guibar
	]
	if $piequest1 [
		guitext "I came across a stranger in the caverns behind the waterfall"
		guitext "He seems very weak and hungry. I offered him the pie the baker's making"
		guitext "I wonder if it's ready yet..."
	] [
		echo "3HEY! NO CHEATING!"
	]
] "A Pie for a Stranger"

newgui npc1-1 [
	if (= $pie1ready 2) [
		guitext "Thanks for the apples, friend." chat
		guitext "I could not have baked those pies without you!" chat
		guibar
	] [
		if (= $pie1ready 1) [
			guitext "You're just in time, friend!" chat
			guitext "I just finished baking your pie" chat
			guibar
			guibutton "ooh! when can I get it!" [showgui npc1-5; pie1ready = 2; pies = (+ $pies 1); totalpies = (+ $totalpies 1); echo "0You obtain an apple pie"] chat
		] [
			if (= $pickedapples1 2) [
				guitext "The pie is in the oven as we speak, come" chat
				guitext "back in a bit, and it'll be done." chat
				guitext "Why don't you go exploring or something" chat
				guitext "while you wait. I heard of a cavern behind a" chat
				guitext "waterfall somewhere"
			] [
				guitext "Hello there, you don't happen to have any" chat
				guitext (format "apples, would you %1?" (getname)) chat
			]
			guibar
			if (< $pickedapples1 2) [
				if (= $pickedapples1 0) [
					guibutton "No, how many do you need?" "showgui npc1-2; pickedapples1 = 1; applequests = (+ $applequests 1); totalquests = (+ $totalquests 1)" chat
				] [
					if (> $apples 14) [
						guibutton "I've got the apples you wanted" [pickedapples1 = 2; showgui npc1-4; apples = (- $apples 15); finishedquests = (+ $finishedquests 1)] chat
					] [
					guibutton "I've got the apples you wanted" [showgui npc1-3] chat
					]
				]
			]
		]
	]
	guibutton "Good bye" [bakerspeak "Good-bye" 2000] chat
] "Baker"

newgui npc1-2 [
	guitext "I need 15 apples to bake some pies" chat
	guitext "apple pies seem to be quite popular these days" chat
	guibar
	guibutton "I'll go look for  few then, I'll be back soon" [bakerspeak "Don't take too long" 3000] chat
] "Baker"

newgui npc1-3 [
	guitext (format "I need 15 apples, you've only got %1." $apples) chat
	guitext "Keep looking, I'm sure you'll find enough" chat
	guibar
	guibutton (format "guess I'll go look for %1 more apples then" (- 15 $apples)) "cleargui" chat
] "Baker"

newgui npc1-4 [
	guitext "Thanks for the apples, I'll have an apple pie" chat
	guitext "baked specially for you, ready soon." chat
	guibar
	guibutton "I'll come pick it up shortly" [bakerspeak "Your pie will be ready soon" 3000] chat
] "Baker"

newgui npc1-5 [
	guitext "You be careful now." chat
	guitext "That pie just came out of the oven so it's hot!" chat
	guibar
	guibutton "Will do, thanks!" [bakerspeak "Come again soon!" 2000] chat
] "Baker"

newgui npc2-1 [
	if (&& (= $piequest1 1) $pies) [
		guitext "(he sniffs the air, and fixates his eyes on you)"
		guibar
		guibutton "(Show him the pie)" "showgui npc2-3"
		guibutton "(Stare back)" [sleep 500 [johnact1 "Chuckles weakly" 3500]]
	] [
		if (= $piequest1 1) [
			guitext "Please... hurry..." chat
			guibar
			guibutton "I will return shortly" [johnspeak1 "Please do" 2000] chat
		] [
			if (= $pickedapples1 2) [
				guitext "Need... food..." chat
				guitext "(you catch his eye)"
				guitext "You there... you got any food?" chat
				guibar
				guibutton "The baker's making me a pie, would you like it?" "pie1ready = 1; piequests = (+ $piequests 1); piequest1 = 1; showgui npc2-2; totalquests = (+ $totalquests 1)" chat
				guibutton "Bye!" [johnact1 "Stares silently at you" 2500] chat
			] [
				guitext "S-s-s-so... c-c-c-cold..." chat
				guitext "Need... f-food..." chat
				guibar
				guibutton "(Say nothing)" "cleargui"
			]
		]
	]
] "Strange man"

newgui npc2-2 [
	guitext "th-th-that'd be m-most welc-c-come" chat
	guitext "thanks..." chat
	guibar
	guibutton "I wonder if it's ready yet..." "cleargui" chat
] "Strange Man"

newgui npc2-3 [
	guitext "(He approaches cautiously at first, then"
	guitext "Hastily grabs the pie and begins eating."
	guitext "He eats hurriedly, and a few tears start forming in his eyes)"
	guibar
	guibutton "(Wait...)" "showgui npc2-4"
] "Strange Man"

newgui npc2-4 [
	guitext "I haven't tasted anything so good, in so long." chat
	guitext "(He wipes the tears with his sleeves)"
	guitext "We haven't been introduced yet, I'm John," chat
	guitext "and who would I thank for bringing me this feast" chat
	guibar
	guibutton (getname) "showgui npc2-5" chat
] "John"

newgui npc2-5 [
	guitext (format "hmm... %1... what a nice name" (getname)) chat
	guitext "Listen, can you come back in a bit" chat
	guitext "so I have a chance to finish the pie" chat
	guibar
	guibutton "I suppose, I'll come back later John" "piequest1 = 2; pies = (- $pies 1); sleep 60000 [finishedpie1 = 1]; finishedquests = (+ $finishedquests 1)" chat
] "John"

newgui npc2-6 [
	if $finishedpie1 [
		guitext (format "Hey! if it isn't %1, my little life saver" (getname)) chat
		guitext "How are you buddy!" chat
		guibar
		if (= $cavequestion 0) [guibutton "What're you doing in this cave?" "showgui npc2-8" chat]
		guibutton "I'm fine, John, mind if I ask you some questions?" "showgui npc2-7" chat
		guitext "See ya around John" chat
	] [
		guitext "(He's eating the pie with a beast like devotion"
		guitext "With the ferrocity he's eating, you consider it's best not to interfere)"
		guibar
		guibutton "(Leave)" "cleargui"
	]
] "John"

newgui npc2-7 [
	guitext "Sure I guess, what do you want to know?" chat
	guibar
	guibutton "What're you doing in this cave?" "showgui npc2-8" chat
	guibutton "Where are you from?" "showgui npc2-9" chat
	guitext "Can I get you anything else?"
	guibutton "Nothing, thanks!" "cleargui" chat
] "John"

newgui npc2-8 [
	cavequestion = 1
	guitext "Life Story Here" chat
	guibar
	guibutton "What happened back then?" "showgui npc2-9" chat
	guibutton "Can I ask you something else?" "showgui npc2-7" chat
	guibutton "Goodbye, John" [johnspeak1 "Of ye go then"]  chat
] "John"

newgui npc2-9 [
	guitext "More of his life story"
	guibar
	guibutton "Can I ask you something else?" "showgui npc2-7" chat
	guibutton "Goodbye, John" [echo "John: Of ye go then"]  chat
]
pickup = [
	if (= (getalias (format "gotapple%1" $arg1)) 0) [
		(format "gotapple%1" $arg1) = 1
		echo "0You've picked up an apple"
		apples = (+ $apples 1)
		totalapples = (+ $totalapples 1)
	] [
		applemesg (rnd 5)
	]
]
pie1ready = [0]
finishedquests = 0
money = 0
applequests = 0
finishedpie1 = 0
johnspeak1 = [part_text_1 = $arg1; sleep $arg2 [part_text_1 = ""]]
cavequestion = 0
totalpies = 0
pies = 0
totalapples = 0
apples = 0
applemesg = [
	if (= $arg1 0) [
		echo "0The apple is beamed up before your eyes... scotty must've been hungry"
	] [
		if (= $arg1 1) [
			echo "0There's um... worms in it, so you just discard the apple"
		] [
			if (= $arg1 2) [
				echo "0A creature from underground grabs the apple before you could"
			] [
				if (= $arg1 3) [
					echo "0it's an imaginary apple!"
				] [
					echo "0this apple's rotten..."
				]
			]
		]
	]
]
