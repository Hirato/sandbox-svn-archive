//gmc - gui moviecube

///TODO: change model directly in gui "new character"

echo "moviecube: reading menus.cfg"
exec data/movie/camera.cfg

_gmc_reset = [
	echo "reseting moviecube gui"
	_gmc_result = ""
	_gmc_character = ""
	_gmc_model_num = 0
	_gmc_slider_speed = 20
	_gmc_wait = 0
	_gmc_field_wp = ""
	_gmc_field_wp_iter = 0
	_gmc_ao_0 = 0
	_gmc_ao_1 = 0
	_gmc_hold = 0
	_gmc_setgesture = 0
	_gmc_gesture_millis = 0
	_gmc_attach_model = ""
	_gmc_attach_bodypart = 0
	_gmc_detach_bodypart = 0
	_gmc_command = ""
	_gmc_cond = ""
	_gmc_condwait_millis = 0
]


_gmc_help = "To get into the MovieCube GUI again, press TAB.^n^nMost of the character commands are supported by this gui. Actions will be saved into the map.cfg file. You can edit them with the ingame editor after that. Since this version cutscenes from the rpg module are supported in MovieCube. Visit our wiki. There should be some documentation there. The next release will come with more models and fully saving support."


newgui gmc_help [
	guititle "Welcome to MovieCube"
	guitextbox $_gmc_help 30 10
	guibar
	guibutton exit [ cleargui ]
]

_gmc_spawnnewcharacter = [
	appendactionmodel $_gmc_character 0 $_gmc_model_num
	appendactionspeed $_gmc_character 0 $_gmc_slider_speed
	appendactionspawn $_gmc_character 0 $_gmc_field_wp
]

_gmc_iter_wp = [
	_gmc_field_wp_iter = ( + $_gmc_field_wp_iter $arg1 )
	if ( < $_gmc_field_wp_iter 0) [
		_gmc_field_wp_iter = (- (listlen (listwaypoints)) 1)
	]

	if ( > $_gmc_field_wp_iter ( - (listlen (listwaypoints) ) 1 ) ) [
		_gmc_field_wp_iter = 0
	]

	_gmc_field_wp = (at (listwaypoints) $_gmc_field_wp_iter)
]

_gmc_get_wp = [
	guilist [
		guistayopen [
			guibutton "prev " [ _gmc_iter_wp -1 ] info
			guibutton "next "  [ _gmc_iter_wp 1 ] info
			guifield _gmc_field_wp 3
		]
	]
]

newgui gmc_new_character [
		guititle $_gmc_character
		guibar
		guilist [
			guilist [
				guitext "model  "
				guitext "speed  "
				guitext "spawn  "
			]
			guilist [
				guibutton (at (listplayermodels) $_gmc_model_num) [ showgui gmc_choose_model ] pointer
				guislider _gmc_slider_speed 5 200 $_gmc_slider_speed
				_gmc_get_wp
			]
			guibar
			guiimage (concatword "packages/models/" (at (listplayermodels) $_gmc_model_num) "/screenshot.jpg") [] 2
		]
		guibar
		guibutton ok [ _gmc_spawnnewcharacter; showgui gmc_edit_character ]
] "new character"

///TODO: let "jump back to new characters automatic
newgui gmc_choose_model [
		guilist [
			guilist [
				loop  i (listlen (listplayermodels)) [
					guibutton (at (listplayermodels) $i) [
						_gmc_model_num = @i
						_gmc_field_wp = (at (listwaypoints) 0)
						showgui gmc_new_character
					]
				]
			]
			guibar
			guiimage (concatword "packages/models/" $guirollovername "/screenshot.jpg") [] 3 0 data/sandboxlogo
		]
] "choose model"

_gmc_edit_actions = [
	guititle $_gmc_character
	guibar
	guilist [
		guilist [
			guitext move
			guitext wait
			guitext setao
			guitext sethold
			guitext setgesture
			guitext attach
			guitext detach
			guitext jump
			guitext destroy
		]
		guilist [
			//move
			_gmc_get_wp

			//wait
			guibutton (concat "millis:" $_gmc_wait) [ _gmc_result = _gmc_wait; showgui gmc_set_millis ] info

			//setao
			guilist [
				guibutton (concat "anim:" $_gmc_ao_0) [ _gmc_result = _gmc_ao_0; showgui gmc_choose_animation ] info
				guibutton (concat "ao:" $_gmc_ao_1) [ _gmc_result = _gmc_ao_1; showgui gmc_choose_animation ] info
			]

			//sethold
			guibutton (concat "anim:" $_gmc_hold) [ _gmc_result = _gmc_hold; showgui gmc_choose_animation ] info

			//setgesture
			guilist [
				guibutton (concat "anim:" $_gmc_setgesture) [ _gmc_result = _gmc_setgesture; showgui gmc_choose_animation ] info
				guibutton (concat "millis:" $_gmc_gesture_millis) [ _gmc_result = _gmc_gesture_millis; showgui gmc_set_millis ] info
			]

			//attach
			guilist [
				guibutton (concat "part:" $_gmc_attach_bodypart) [ _gmc_result = _gmc_attach_bodypart; showgui gmc_choose_bodypart ] info
				guibutton model [ showgui gmc_choose_attachment ] info
			]

			//detach
			guibutton (concat "part:" $_gmc_detach_bodypart) [ _gmc_result = _gmc_detach_bodypart; showgui gmc_choose_bodypart ] info

			//guibutton destroy [] info
			//guibutton destroy [] info
		]
		guilist [
			guibutton "[add]" [ appendactionproxy appendactionmove $_gmc_character 0 $_gmc_field_wp ] none
			guibutton "[add]" [ appendactionproxy appendactionwait $_gmc_character 0 $_gmc_wait ] none
			guibutton "[add]" [ appendactionproxy appendactionsetao $_gmc_character 0 $_gmc_ao_0 $_gmc_ao_1 ] none
			guibutton "[add]" [ appendactionproxy appendactionsethold $_gmc_character 0 $_gmc_hold ] none
			guibutton "[add]" [ appendactionproxy appendactionsetgesture $_gmc_character 0 $_gmc_setgesture; appendactiongesture $_gmc_character 0 $_gmc_gesture_millis ] none
			guibutton "[add]" [ appendactionproxy appendactionattach $_gmc_character 0 $_gmc_attach_bodypart $_gmc_attach_model 0 ] none
			guibutton "[add]" [ appendactionproxy appendactiondetach $_gmc_character 0 $_gmc_detach_bodypart ] none
			guibutton "[add]" [ appendactionproxy appendactionjump $_gmc_character 0 ] none
			guibutton "[add]" [ appendactionproxy appendactiondestroy $_gmc_character 0 ] none
		]
	]
	guibar
	guibutton exit [ cleargui; _gmc_reset ]
]

_gmc_edit_scripting = [
	guititle $_gmc_character
	guibar
	guilist [
		guiradio command i 0 [ showgui _gmc_edit_scripting]
		guiradio conditional i 1 [ showgui _gmc_edit_scripting]
		guiradio condwait i 2 [ showgui  _gmc_edit_scripting]
	]
	guibar
	if ( > $i 0 ) [
		guitext condition
		guifield _gmc_cond 30
	]
	if ( < $i 2) [
		guitext command
		guieditor _gmc_command 30 10
	]
	guibar
	guilist [
		guibutton add [
			textalias _gmc_command _gmc_command
			if ( = $i 0 ) [ appendactionproxy appendactioncommand $_gmc_character 0 $_gmc_command ]
			if ( = $i 1 ) [ appendactionproxy appendactioncond $_gmc_character 0 $_gmc_cond $_gmc_command ]
			if ( = $i 2 ) [ appendactionproxy appendactioncondwait $_gmc_character 0  $_gmc_cond ]
		]
		guibutton exit [ cleargui; _gmc_reset ]
	]
]

newgui gmc_edit_character [
	guistayopen [
		guinoautotab [
			_gmc_edit_actions
			guitab "scripting"
			_gmc_edit_scripting
		]
	]
] "edit character"

newgui gmc_toplevel_characters [
	guilist [
		guibutton "save actions" [ saveactions; cleargui; ]
		guibutton "clear actions" [ clearactions; cleargui; ]
	]
	guibar
	if ($editing) [
		guitext "add a new character" info
		guilist [
			guitext "name: "
			guifield s 15 [if (< 0 (strlen $s)) [
				_gmc_character = $s; s = ""
				if ( (appendactionproxy newcharacter $_gmc_character) ) [ showgui gmc_new_character ]
				]
			]
		]
		guibar
		loop i ( listlen (listcharacters) ) [
			guibutton ( at (listcharacters) $i ) [
				_gmc_character = ( at (listcharacters) @i )
				showgui gmc_edit_character
			]
		]
	][
		guitext "spawn character" info
		loop i ( listlen (listcharacters) ) [
			guibutton ( at (listcharacters) $i ) [
				addcharacter (at (listcharacters) @i )
			]
		]
	]
	guibar
	guilist [
		guibutton exit [ cleargui ]
		guibutton help [ showgui gmc_help ]
		guibutton avi-record [ showgui recorder ]
	]
] characters

newgui gmc_choose_animation [
	loop j ( listlen (listanimations $_gmc_model_num) ) [
		guibutton ( at (listanimations $_gmc_model_num) $j ) [
			alias  $_gmc_result ( getanimation @_gmc_model_num ( at (listanimations $_gmc_model_num) @j ) )
			showgui gmc_edit_character
		]
	]
] "choose animation"

newgui gmc_choose_bodypart [
	loop j ( listlen (listattachpoints) ) [
		guibutton ( at (listattachpoints) $j ) [
			alias  $_gmc_result ( getattachnum ( at (listattachpoints) @j ) )
			showgui gmc_edit_character
		]
	]
] "choose bodypart"

newgui gmc_choose_attachment [
	guifield _gmc_attach_model 50
	guibar
	guibutton ok [ showgui gmc_edit_character ]
] "attach model"

newgui gmc_set_millis [
	guislider i 0 10000
	guibar
	guibutton ok [
		alias  $_gmc_result $i
		showgui gmc_edit_character
	]
] "set duration"

mainmaps = "moviecubedemo"

newgui maps [
	guilist [
		guilist [
			genmapitems $mainmaps
		]
		showmapshot movie
	]

	guitab "Global Maps"
	guilist [
		guilist [
			genmapitems $globalmaps
		]
		showmapshot
	]

	guitab "My maps"

	mapnum = (listlen $maps)
	mapindex = (max 0 (min $mapindex (- $mapnum (- $guiautotab 2))));

	guilist [
		guilist [
			guilist [
				guistayopen [guibutton "Refresh" initmapgui]
				guistrut 24
			]
			guibar
			loop i (- $guiautotab 2) [
				if (> $mapnum (+ $mapindex $i)) [
					index = (+ $i $mapindex);
					guibutton (at $maps $index) (format "map ^"/%1^"" (at $maps $index))
				]
			]
			if (! $mapnum) [guitext "No custom maps"]
		]
		if (> $mapnum (- $guiautotab 2)) [
			guibar
			guislider mapindex 0 (- $mapnum (- $guiautotab 2)) "" "" 1
		]
		showmapshot
	]
] "Main Maps"


initmapgui = [
	createmaplist $mainmaps
]
