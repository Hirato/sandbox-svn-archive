//static camera gui - using cutscenes

//caution: it becomes a bit confusing if cutscene number and tag number differ
//have in mind, that cutscene (camera) number is not the tag number

echo "moviecube: reading camera.cfg"

_gmc_camera_reset = [
	_gmc_camera_tag = 0
	_gmc_camera_pitch = 0
	_gmc_camera_yaw = 0
	_gmc_curcamera = 0
]

_gmc_staticcamera = 0

_gmc_camera_reset

//arg1 = tag
_gmc_add_static_camera = [
	r_new_cutscene [
		r_cutscene_movecamera $arg1 0
		r_cutscene_viewcamera $arg1 0 [ 
			r_cutscene_cond [ 
				if (= $_gmc_staticcamera 1) [ 
					result 1 
				]
			]
		]
	]
]

//arg1 = camera idx
_gmc_switch_camera = [
	_gmc_staticcamera = 0
	r_start_cutscene $arg1
]

newgui gmc_create_camera [
	if ($editing) [
		guilist [
			guitext "camera tag:"
			guislider _gmc_camera_tag 0 20
		]
		guilist [
			guitext "camera pitch:"
			guislider _gmc_camera_pitch 0 20
		]
		guilist [
			guitext "camera yaw:"
			guislider _gmc_camera_yaw 0 20
		]
		guibar
		guilist [
			guibutton ok [ 
				newent camera $_gmc_camera_tag $_gmc_camera_pitch $_gmc_camera_yaw
				_gmc_camera_reset
				cleargui 
			]
			guibutton cancel [ cleargui ]
		]
	] [
		guitext "press ^'E^' to switch into editmode before creating a camera"
		guibutton exit [ cleargui ]
	]
] "create static camera"

newgui gmc_edit_camera [
	guitext "edit static cameras"
	guislider _gmc_camera_pitch -360 360 [ entset camera $_gmc_camera_tag $_gmc_camera_pitch $_gmc_camera_yaw; _gmc_switch_camera $_gmc_curcamera ]
	guislider _gmc_camera_yaw -90 90  [ entset camera $_gmc_camera_tag $_gmc_camera_pitch $_gmc_camera_yaw; _gmc_switch_camera $_gmc_curcamera ]
	guibar
	guibutton exit [
		_gmc_camera_reset
		_gmc_staticcamera = 1
		entcancel
		cleargui
	]
] "edit static camera"

//arg1 = idx, //arg2 = tag
_gmc_edit_static_camera = [
	if ($editing) [
		 entfind camera $arg2
		if  (= (enthavesel) 1) [
			_gmc_curcamera = $arg1
			 entloop [ _gmc_camera_tag = (at (entget) 1) ]
			 entloop [ _gmc_camera_pitch = (at (entget) 2) ]
			 entloop [ _gmc_camera_yaw = (at (entget) 3) ]
			_gmc_switch_camera $arg1
			showgui gmc_edit_camera
		][
			echo canceled - more than one camera with the same tag
		]
	][
		guitext "press ^'E^' to switch into editmode before editing a camera"
		guibutton exit [ 
			_gmc_camera_reset
			cleargui 
		]
	]
]

alias newcameraentity [ showgui gmc_create_camera ]
alias addstaticcamera [ _gmc_add_static_camera $arg1 ]
alias editstaticcamera [ _gmc_edit_static_camera $arg1 $arg2 ]
alias switchstaticcamera [ _gmc_switch_camera $arg1 ]
alias exitstaticcamera [ _gmc_staticcamera = 1 ]
